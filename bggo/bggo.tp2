BACKUP ~bggo/backup~
AUTHOR ~Yovaneth, White Agnus, Weigo~
VERSION ~v3.0 beta~

ALWAYS
	INCLUDE ~%MOD_FOLDER%/lib/a7_tools.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/macros.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/eet_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/g3_bgt_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/g3_tutu_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/g3_bgee_cpmvars.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	INCLUDE ~%MOD_FOLDER%/lib/debug.tpa~

	ACTION_IF GAME_IS ~eet bgee bg2ee~ BEGIN
		OUTER_SPRINT ee ~ee~
	END

	ACTION_IF GAME_IS ~bgt tutu_totsc tob~ BEGIN
		OUTER_SPRINT ee ~non-ee~
	END
	
	// remove bif directory
	ACTION_IF (DIRECTORY_EXISTS ~%MOD_FOLDER%/bif~) BEGIN
		ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
			AT_NOW ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
		END ELSE BEGIN //osx, unix
			AT_NOW ~rm -rf %MOD_FOLDER%/bif~ EXACT
		END
	END
	ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
		AT_EXIT ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
	END ELSE BEGIN //osx, unix
		AT_EXIT ~rm -rf %MOD_FOLDER%/bif~ EXACT
	END
	ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
		AT_UNINSTALL ~rmdir /s /q %MOD_FOLDER%\bif~ EXACT
	END ELSE BEGIN //osx, unix
		AT_UNINSTALL ~rm -rf %MOD_FOLDER%/bif~ EXACT
	END
	
	// remove old goXXXX.bif in data directory
	// prevents a long loading time, if a bif file should still be present in the data folder during installation
	// close the program (e.g. Near Infinity, weidu)
	ACTION_TRY
		ACTION_BASH_FOR ~data~ ~^go.*\.bif$~ BEGIN
			DELETE + ~%BASH_FOR_FILESPEC%~
		END
	WITH
		DEFAULT
			PRINT ~**********************************************************************************~
			PRINT ~The %BASH_FOR_FILESPEC% is still in use by a program. e.g.: Near Infinity or WeiDU%LNL%Please close the programs and try again~
			PRINT ~**********************************************************************************~
			ACTION_RERAISE
	END
END



/////////////////////////////
BEGIN ~BGGO for EET, BGT, BGEE, BG2EE, ToB and Tutu~
REQUIRE_PREDICATE GAME_IS ~eet bgt tutu_totsc bgee bg2ee tob ~ ~This mod is currently for EET, BGT, BGEE, BG2EE, ToB and Tutu~

ACTION_DEFINE_ARRAY bg1_areas BEGIN
	//outdoor
	BG0100 BG0200 BG0300 BG0400 BG0500 BG0600 BG0621 BG0700 BG0800 BG0900 
	BG1000 BG1008 BG1100 BG1200 BG1300 BG1400 BG1500 BG1600 BG1700 BG1800
	BG1900 BG2000 BG2100 BG2200 BG2300 BG2400 BG2600 BG2626 BG2700 BG2800 
	BG2900 BG3000 BG3100 BG3200 BG3300 BG3400 BG3500 BG3600 BG3700 BG3800 
	BG3900 BG4000 BG4100 BG4200 BG4300 BG4400 BG4500 BG4600 BG4700 BG4800 
	BG4900 BG5000 BG5100 BG5200 BG5300 BG5400 BG5500 BG5600 BG5700 BG5800 
	BG5900 BG6000 BG6100 
	// indoor
	BG0101 BG0102 BG0103 BG0108 BG0109 BG0110 BG0111 BG0112 BG0123 BG0126 
	BG0127 BG0128 BG0129 BG0130 BG0131 BG0138 BG0139 BG0140 BG0141 BG0142 
	BG0146 BG0162 BG0165 BG0166 BG0169 BG0170 BG0224 BG0225 BG0226 BG0304 
	BG0506 BG0511 BG0512 BG0513 BG0514 BG0601 BG0602 BG0603 BG0607 BG0611 
	BG0612 BG0613 BG0614 BG0615 BG0616 BG0719 BG0720 BG0807 BG0808 BG1003 
	BG1004 BG1005 BG1009 BG1101 BG1102 BG1207 BG1208 BG1215 BG1320 BG1802 
	BG1804 BG2301 BG2302 BG2303 BG2608 BG3202 BG3301 BG3307 BG3308 BG3313 
	BG3314 BG3327 BG3328 BG4004
END

ACTION_DEFINE_ARRAY bg2_tob_areas BEGIN
	// outdoor
	AR0042 AR0043 AR0044 AR1100 AR1200 AR1300 AR1304 AR1500 AR1600 AR1700
	AR1800 AR2500 AR2600 AR2700 AR2800 AR2806 AR3000 AR4000 AR5000 AR5200 
	AR5500 AR6000 AR6100 AR6300 AR6400
	// indoor
	AR1102 
END

ACTION_DEFINE_ARRAY sod_areas BEGIN
	// outdoor
	BD0010 BD0101 
	// indoor
	
END

ACTION_DEFINE_ARRAY bgee_areas BEGIN
	// outdoor
	OH2000
	// indoor
	
END

ACTION_DEFINE_ARRAY bg2ee_areas BEGIN
	// outdoor
	OH4010 OH4290 OH5400
	// indoor
END



LAF ~INSTALL_MAIN~ 
	INT_VAR debug = 0  // list of all new used files
END



/////////////////////////////
BEGIN ~Use extended BG1 areas for TDDz~
REQUIRE_PREDICATE (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "2") OR (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "3") OR (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "4") "No TDD component detected."
REQUIRE_COMPONENT "BGGOEET/SETUP-BGGOEET.TP2" "0" "The main component must be installed before" 

ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_tis_tdd BEGIN
	"DD1900"    => "%BanditCamp%"
	"DD2300"    => "%FriendlyArmInn%"
	"DD3300"    => "%Beregost%"
	"DD8000"    => "%Larswood%"
END

ACTION_PHP_EACH remapped_tis_tdd AS TDDARE => BG1ARE BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%TDDARE%.are~ BEGIN
		COPY_EXISTING ~%TDDARE%.are~ ~override~
		PATCH_IF SOURCE_SIZE > 0x11b BEGIN
			WRITE_ASCIIE 0x8 ~%BG1ARE%~ #8
			WRITE_BYTE 0x48 (THIS BOR 0b01000000)
		END
		BUT_ONLY
	END
END
