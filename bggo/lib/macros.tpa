INCLUDE ~%MOD_FOLDER%/lib/remapped_tis.tpa~

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_anim_at_night BEGIN
		AR1200TN => BG1200
		AR900WN1 => BG0900
		AR900WN2 => BG0900
		AR900WN4 => BG0900
		AR900WD1 => BG0900
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN
		bird => 1
		eagle => 1
		vultur => 1
		seagul => 1
END

DEFINE_PATCH_MACRO ~DELETE_EXISTING_ANIMATIONS~
BEGIN
	GET_OFFSET_ARRAY animation_array ARE_V10_ANIMATIONS
	PATCH_PHP_EACH animation_array AS animation_num => animation_offset BEGIN
		READ_SHORT animation_offset+0x20 animation_pos_x
		READ_SHORT animation_offset+0x22 animation_pos_y
		PATCH_IF ((~%animation_pos_x%~ = $x_coord("%index%")) AND (~%animation_pos_y%~ =  $y_coord("%index%"))) BEGIN
			LPF fj_are_structure INT_VAR fj_delete_mode = animation_num	STR_VAR fj_structure_type = animation END
		END
	END
END


DEFINE_ACTION_FUNCTION ~INSTALL_TILESETS~
	STR_VAR
		purple = ~~
BEGIN

	ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%are%~) OR FILE_EXISTS_IN_GAME ~%are%.are~) BEGIN
		ACTION_IF (~%purple%~ STRING_EQUAL ~BG1~) BEGIN
			OUTER_TEXT_SPRINT are2 $EVAL ~remapped_tis~(~%are%~)
		END
		ACTION_IF (~%purple%~ STRING_EQUAL ~TOB~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		ACTION_IF (~%purple%~ STRING_EQUAL ~SOD~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		
		
		ACTION_IF FILE_EXISTS_IN_GAME ~%are2%.are~ BEGIN
		
			// Extended night & creatures sleep at night
			COPY_EXISTING ~%are2%.are~ ~override~
			PATCH_IF SOURCE_SIZE > 0x11b BEGIN // Valid size check
				PATCH_IF (((BYTE_AT 0x48) << 7) BAND BIT7) BEGIN // Toggle only outdoor areas
					WRITE_BYTE 0x48 ((THIS BOR BIT6) BOR BIT1) // Toggle extended night flag (BIT6) and day/night flag (BIT1) on whether on or off
					GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110
					PHP_EACH actors AS i => off BEGIN
						READ_ASCII off + 0x80 cre_resref
						TO_LOWER cre_resref
						PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
							WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
						END
					END
					GET_OFFSET_ARRAY animations 0xb0 4 0xac 4 0 0 0x4c
					PHP_EACH animations AS i => off BEGIN
						READ_ASCII off + 0x0028 animation
						TO_UPPER animation
						PATCH_IF VARIABLE_IS_SET $turn_off_anim_at_night("%animation%") BEGIN
								WRITE_LONG off+0x34 0x00
						END
						
					END
				END
			END
			BUT_ONLY
								
			MKDIR ~%MOD_FOLDER%/bif/%are2%~
			
			//**************************************************
			// Day files
			//**************************************************
			
			// day tis v1 or tis v2 file 
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~) BEGIN
				ACTION_IF GAME_IS ~bgt tutu_totsc bg2~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ BEGIN
						LAF HANDLE_TILECONV
							STR_VAR
								input_path    = EVAL ~%MOD_FOLDER%/base/tbcD/%are%/%ee%~
								output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
						END
						ACTION_IF (~%are%~ STR_EQ ~%are2%~)=0 BEGIN 
							MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%.tis~
						END
					END
				END
				ACTION_IF GAME_IS ~eet bgee bg2ee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ BEGIN
						COPY + ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%.tis~
						ACTION_IF (GAME_IS ~bgee~ AND (~%purple%~ STRING_EQUAL ~BG1~)) BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								OUTER_PATCH_SAVE newname ~%BASH_FOR_RES%~ BEGIN
									REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~B~ ~A~
								END
								COPY ~%BASH_FOR_FILESPEC%~ ~%MOD_FOLDER%/bif/%are2%/%newname%.pvrz~
							END
						END ELSE BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								COPY ~%BASH_FOR_FILESPEC%~ ~%MOD_FOLDER%/bif/%are2%~
							END
						END
					END
				END
			END
			
			
			// day mos file
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/mbcD/%ee%/%are%/%are%.mbc~ BEGIN			
				LAF HANDLE_TILECONV
					STR_VAR
						input_path = EVAL ~%MOD_FOLDER%/base/mbcD/%ee%/%are%~
						output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
				END
				ACTION_IF GAME_IS ~bgee bgt tutu~ AND (~%are%~ STR_EQ ~%are2%~)=0 BEGIN
					MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%.mos~
				END
			END ELSE BEGIN
				COPY_EXISTING ~%are2%.mos~ ~%MOD_FOLDER%/bif/%are2%~
			END
			
			// day lightmap
			COPY_EXISTING ~%are2%LM.bmp~ ~%MOD_FOLDER%/bif/%are2%~
			
			// day wed file
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ ~%MOD_FOLDER%/bif/%are2%/%are2%.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
			END ELSE BEGIN
				COPY_EXISTING ~%are2%.wed~ ~%MOD_FOLDER%/bif/%are2%~
			END

			//**************************************************
			// Night files
			//**************************************************		
			
			// night tis v1 or tis v2
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~) BEGIN
				ACTION_IF GAME_IS ~bgt tutu_totsc tob bg2~ BEGIN
					LAF HANDLE_TILECONV
						STR_VAR
							input_path    = EVAL ~%MOD_FOLDER%/base/tbcN/%are%/%ee%~
							output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
					END
					ACTION_IF (~%are%~ STR_EQ ~%are2%~)=0 BEGIN 
						MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%N.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.tis~
					END
				END
				ACTION_IF GAME_IS ~eet bgee bg2ee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ BEGIN
						COPY + ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.tis~
						ACTION_IF (GAME_IS ~bgee~ AND (~%purple%~ STRING_EQUAL ~BG1~)) BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								OUTER_PATCH_SAVE newname ~%BASH_FOR_RES%~ BEGIN
									REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~B~ ~A~
								END
								COPY ~%BASH_FOR_FILESPEC%~ ~%MOD_FOLDER%/bif/%are2%/%newname%.pvrz~
							END
						END ELSE BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								COPY ~%BASH_FOR_FILESPEC%~ ~%MOD_FOLDER%/bif/%are2%~
							END
						END
					END
				END
			END
			
			// night mos
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/mbcN/%ee%/%are%N/%are%N.mbc~ BEGIN						
				LAF HANDLE_TILECONV
					STR_VAR
						input_path    = EVAL ~%MOD_FOLDER%/base/mbcN/%ee%/%are%N~
						output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
				END
				ACTION_IF GAME_IS ~bgee bgt tutu~ AND (~%are%~ STR_EQ ~%are2%~)=0 BEGIN
					MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%N.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.mos~
				END
			END
			
			// night lightmap
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpLN/%ee%/%are%LN.bmp~ BEGIN		
				COPY ~%MOD_FOLDER%/base/bmpLN/%ee%/%are%LN.bmp~ ~%MOD_FOLDER%/bif/%are2%/%are2%LN.bmp~
			END ELSE BEGIN
				ACTION_IF FILE_EXISTS_IN_GAME ~%are2%LN.bmp~ BEGIN
					COPY_EXISTING ~%are2%LN.bmp~ ~%MOD_FOLDER%/bif/%are2%~
				END
			END 
			
			// searchmap
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpSR/%are%SR.2da~ BEGIN
				LAF ALTER_SEARCHMAP
					STR_VAR
						path_to_2da_file = EVAL ~%MOD_FOLDER%/base/bmpSR/%are%SR.2da~ // full path to the *changes.2da file, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
						areaname 		 = EVAL ~%are2%~			                  // area name, e.g. ~AR3700~
				END
			END
			COPY_EXISTING ~%are2%SR.bmp~ ~%MOD_FOLDER%/bif/%are2%~
								
			// heightmap
			COPY_EXISTING ~%are2%HT.bmp~ ~%MOD_FOLDER%/bif/%are2%~
				
			// night wed
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
					PATCH_IF ((~%are%~ STRING_COMPARE_CASE "%Beregost%")=0 AND FILE_EXISTS ~override/GavinNPC.B~) BEGIN
						PATCH_PRINT ~Gavin detected~
						PATCH_INCLUDE ~%MOD_FOLDER%/lib/gavin_patch.tpa~
						LPM ~patchGavin~
					END
			END
			
			MAKE_BIFF ~go%are2%~ BEGIN ~%MOD_FOLDER%/bif/%are2%~ ~^.*$~ END
			
		END
	END
END

DEFINE_ACTION_FUNCTION ~INSTALL_MAIN~
BEGIN
	MKDIR ~%MOD_FOLDER%/bif~
	
	// tis files for water overlays
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/ys~ ~^.+\.tis$~ BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
			COPY + ~%MOD_FOLDER%/base/tbcN/ys/%BASH_FOR_FILE%~ ~override~
		END
	END
	
	// bam animations
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/bam~ ~^.+\.bam$~ BEGIN
		COPY + ~%MOD_FOLDER%/base/bam/%BASH_FOR_FILE%~ ~override~
	END

	//	BG1 Areas
	PRINT ~BG1 Areas~
	ACTION_IF GAME_IS ~eet bgee bgt tutu~ BEGIN
		ACTION_FOR_EACH are IN  ~BG0100~ ~BG0200~ ~BG0300~ ~BG0400~ ~BG0500~ ~BG0600~ ~BG0621~ ~BG0700~ ~BG0800~ ~BG0900~ 
								~BG1000~ ~BG1008~ ~BG1009~ ~BG1100~ ~BG1200~ ~BG1300~ ~BG1400~ ~BG1500~ ~BG1600~ ~BG1700~ 
								~BG1800~ ~BG1900~ ~BG2000~ ~BG2100~ ~BG2200~ ~BG2300~ ~BG2400~ ~BG2600~ ~BG2700~ ~BG2800~ 
								~BG2900~ ~BG3000~ ~BG3100~ ~BG3200~ ~BG3300~ ~BG3400~ ~BG3500~ ~BG3600~ ~BG3700~ ~BG3800~
								~BG3900~ ~BG4000~ ~BG4100~ ~BG4200~ ~BG4300~ ~BG4400~ ~BG4500~ ~BG4600~ ~BG4700~ ~BG4800~ 
								~BG4900~ ~BG5000~ ~BG5100~ ~BG5200~ ~BG5300~ ~BG5400~ ~BG5500~ ~BG5600~ ~BG5700~ ~BG5800~ 
								~BG5900~ ~BG6000~ ~BG6100~ BEGIN
			LAF ~INSTALL_TILESETS~ STR_VAR purple = ~BG1~ END 
		END
	END
	
	// BG2+TOB Areas
	ACTION_IF GAME_IS ~eet bgt bg2ee~ BEGIN
		PRINT ~BG2 + ToB Areas~
		ACTION_FOR_EACH are IN  ~AR0042~ ~AR0043~ ~AR0044~ BEGIN
			LAF ~INSTALL_TILESETS~ STR_VAR purple = ~TOB~ END 
		END
	END
	
	// SoD Areas
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		PRINT ~SoD Areas~
		ACTION_FOR_EACH are IN  ~BD0101~ BEGIN
			LAF ~INSTALL_TILESETS~ STR_VAR purple = ~SOD~ END 
		END
	END
	
	// Add torches, fires, candles and animations
	INCLUDE ~%MOD_FOLDER%/base/animation/Candlekeep.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/BaldursGateCity.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/WyrmsCrossing.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/IceIsland.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/FriendlyArmInn.tpa~
	

	// Light Maps (indoor)
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/bmpLM~ ~^.+$~ BEGIN
		ACTION_TO_UPPER BASH_FOR_RES
        ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~bmp~ BEGIN //BMP
            OUTER_SET updated = 0
            ACTION_FOR_EACH var IN LM BEGIN
                ACTION_IF (updated = 0) BEGIN
                    OUTER_PATCH_SAVE temp ~%BASH_FOR_RES%~ BEGIN
                        REPLACE_TEXTUALLY ~%var%$~ ~~
                    END
                    ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%temp%~)) BEGIN
                        OUTER_TEXT_SPRINT output $EVAL ~remapped_tis~(~%temp%~)
                        //MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%output%%var%.%BASH_FOR_EXT%~
						COPY ~%MOD_FOLDER%/base/bmpLM/%temp%LM.bmp~ ~override/%output%LM.bmp~
                        OUTER_SET updated = 1
                    END
                END
            END
        END
	END
END


