INCLUDE ~%MOD_FOLDER%/lib/remapped_tis.tpa~

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_anim_at_night BEGIN
		AR1200TN => BG1200
		AR900WN1 => BG0900
		AR900WN2 => BG0900
		AR900WN4 => BG0900
		AR900WD1 => BG0900
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN
		bird => 1
		eagle => 1
		vultur => 1
		seagul => 1
END


DEFINE_ACTION_FUNCTION ~INSTALL_TILESETS~
	STR_VAR
		purple = ~~
BEGIN

	ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%are%~)) BEGIN
		OUTER_TEXT_SPRINT are2 $EVAL ~remapped_tis~(~%are%~)
		
		ACTION_IF FILE_EXISTS_IN_GAME ~%are2%.are~ BEGIN
		
			// Extended night & creatures sleep at night
			COPY_EXISTING ~%are2%.are~ ~override~
			PATCH_IF SOURCE_SIZE > 0x11b BEGIN // Valid size check
				WRITE_BYTE 0x48 (THIS BOR 0b01000000) // Toggle extended night flag on whether on or off
				GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110
				PHP_EACH actors AS i => off BEGIN
					READ_ASCII off + 0x80 cre_resref
					TO_LOWER cre_resref
					PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
						WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
					END
				END
				GET_OFFSET_ARRAY animations 0xb0 4 0xac 4 0 0 0x4c
				PHP_EACH animations AS i => off BEGIN
					READ_ASCII off + 0x0028 animation
					TO_UPPER animation
					PATCH_IF VARIABLE_IS_SET $turn_off_anim_at_night("%animation%") BEGIN
							WRITE_LONG off+0x34 0x00
					END
					
				END
			END
			BUT_ONLY
								
			MKDIR ~%MOD_FOLDER%/bif/%are2%~

			//**************************************************
			// Day files
			//**************************************************
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~) BEGIN
				
				// day tis v1 or tis v2 file 
				ACTION_IF GAME_IS ~bgt tutu_totsc~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ BEGIN
						LAF HANDLE_TILECONV
							STR_VAR
								input_path    = EVAL ~%MOD_FOLDER%/base/tbcD/%are%/%ee%~
								output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
						END		
						MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%.tis~
					END
				END
				ACTION_IF GAME_IS ~eet bgee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ BEGIN
						COPY + ~%MOD_FOLDER%/base/tbcD/%are%/%ee%~ ~%MOD_FOLDER%/bif/%are2%~
							   
					END
				END
				
				// day mos file
				//COPY ~%MOD_FOLDER%/base/mosD/%ee%/%are%.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%.mos~
				LAF HANDLE_TILECONV
					STR_VAR
						input_path = EVAL ~%MOD_FOLDER%/base/mbcD/%ee%/%are%~
						output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
				END
				ACTION_IF !GAME_IS ~eet~ BEGIN
					MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%.mos~
				END
				
				// day lightmap
				COPY_EXISTING ~%are2%LM.bmp~ ~%MOD_FOLDER%/bif/%are2%~
			
			END
					
			// day wed file
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ ~%MOD_FOLDER%/bif/%are2%/%are2%.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
			END ELSE BEGIN
				COPY_EXISTING ~%are2%.wed~ ~%MOD_FOLDER%/bif/%are2%~
			END

			//**************************************************
			// Night files
			//**************************************************		
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~) BEGIN
				
				// night tis v1 or tis v2
				ACTION_IF GAME_IS ~bgt tutu_totsc~ BEGIN
					LAF HANDLE_TILECONV
						STR_VAR
							input_path    = EVAL ~%MOD_FOLDER%/base/tbcN/%are%/%ee%~
							output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
					END
					MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%N.tis~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.tis~
				END
				ACTION_IF GAME_IS ~eet bgee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ BEGIN
						COPY + ~%MOD_FOLDER%/base/tbcN/%are%/%ee%~ ~%MOD_FOLDER%/bif/%are2%~
					END
				END
				
				// night mos
				//COPY ~%MOD_FOLDER%/base/mosN/%ee%/%are%N.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.mos~
				LAF HANDLE_TILECONV
					STR_VAR
						input_path    = EVAL ~%MOD_FOLDER%/base/mbcN/%ee%/%are%N~
						output_path	  = EVAL ~%MOD_FOLDER%/bif/%are2%~
				END
				ACTION_IF !GAME_IS ~eet~ BEGIN
					MOVE + ~%MOD_FOLDER%/bif/%are2%/%are%N.mos~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.mos~
				END
				
				// night lightmap
				COPY ~%MOD_FOLDER%/base/bmpN/%ee%/%are%LN.bmp~ ~%MOD_FOLDER%/bif/%are2%/%are2%LN.bmp~
				
				// searchmap
				ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpSR/%are%SR.2da~ BEGIN
					LAF ALTER_SEARCHMAP
						STR_VAR
							path_to_2da_file = EVAL ~%MOD_FOLDER%/base/bmpSR/%are%SR.2da~ // full path to the *changes.2da file, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
							areaname 		 = EVAL ~%are2%~			                  // area name, e.g. ~AR3700~
					END
				END
				COPY_EXISTING ~%are2%SR.bmp~ ~%MOD_FOLDER%/bif/%are2%~
								
				// heightmap
				COPY_EXISTING ~%are2%HT.bmp~ ~%MOD_FOLDER%/bif/%are2%~
			
			END
			
			// night wed
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ ~%MOD_FOLDER%/bif/%are2%/%are2%N.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
					PATCH_IF ((~%are%~ STRING_COMPARE_CASE "%Beregost%")=0 AND FILE_EXISTS ~override/GavinNPC.B~) BEGIN
						PATCH_PRINT ~Gavin detected~
						PATCH_INCLUDE ~%MOD_FOLDER%/lib/gavin_patch.tpa~
						LPM ~patchGavin~
					END
			END
			
			MAKE_BIFF ~go%are2%~ BEGIN ~%MOD_FOLDER%/bif/%are2%~ ~^.*$~ END

		END
	END
END

DEFINE_ACTION_FUNCTION ~INSTALL_MAIN~
BEGIN
	MKDIR ~%MOD_FOLDER%/bif~
	
	ACTION_IF GAME_IS ~bgt tutu_totsc~ BEGIN
		LAF HANDLE_TILECONV
			STR_VAR
				input_path    = EVAL ~%MOD_FOLDER%/base/tbcN/ys/%ee%~
				output_path	  = ~%MOD_FOLDER%/bif/bggom~
		END
	END
	
	ACTION_IF GAME_IS ~eet bgee~ BEGIN
		ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/ys/%ee%~ ~^.+\.tis$~ BEGIN
			ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
				COPY + ~%MOD_FOLDER%/base/tbcN/ys/%ee%/%BASH_FOR_FILE%~ ~override~
			END
		END
	END
	
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/bam~ ~^.+\.bam$~ BEGIN
		//ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
			ACTION_IF !GAME_IS ~eet~ BEGIN
				COPY + ~%MOD_FOLDER%/base/bam/%BASH_FOR_FILE%~ ~%MOD_FOLDER%/bif/bggom~
			END ELSE BEGIN
				COPY + ~%MOD_FOLDER%/base/bam/%BASH_FOR_FILE%~ ~override~
			END
		//END
	END

	MAKE_BIFF ~goMAIN~ BEGIN ~%MOD_FOLDER%/bif/bggom~ ~^.*$~ END

	
	ACTION_FOR_EACH are IN  ~BG0100~ ~BG0200~ ~BG0300~ ~BG0400~ ~BG0500~ ~BG0600~ ~BG0621~ ~BG0700~ ~BG0800~ ~BG0900~ 
							~BG1000~ ~BG1100~ ~BG1200~ ~BG1300~ ~BG1400~ ~BG1500~ ~BG1600~ ~BG1700~ ~BG1800~ ~BG1900~ 
							~BG2000~ ~BG2100~ ~BG2200~ ~BG2300~ ~BG2400~ ~BG2600~ ~BG2700~ ~BG2800~ ~BG2900~ ~BG3000~ 
							~BG3100~ ~BG3200~ ~BG3300~ ~BG3400~ ~BG3500~ ~BG3600~ ~BG3700~ ~BG3800~ ~BG3900~ ~BG4000~ 
							~BG4100~ ~BG4200~ ~BG4300~ ~BG4400~ ~BG4500~ ~BG4600~ ~BG4700~ ~BG4800~ ~BG4900~ ~BG5000~ 
							~BG5100~ ~BG5200~ ~BG5300~ ~BG5400~ ~BG5500~ ~BG5600~ ~BG5700~ ~BG5800~ ~BG5900~ ~BG6000~ 
							~BG6100~ BEGIN
		LAF ~INSTALL_TILESETS~ STR_VAR purple = ~~ END 
	END
	
	// Add torches, fires, candles and animations
	INCLUDE ~%MOD_FOLDER%/base/animation/Candlekeep.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/BaldursGateCity.tpa~
	INCLUDE ~%MOD_FOLDER%/base/animation/WyrmsCrossing.tpa~
	

	// Light Maps (indoor)
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/bmpLM~ ~^.+$~ BEGIN
		ACTION_TO_UPPER BASH_FOR_RES
        ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~bmp~ BEGIN //BMP
            OUTER_SET updated = 0
            ACTION_FOR_EACH var IN LM BEGIN
                ACTION_IF (updated = 0) BEGIN
                    OUTER_PATCH_SAVE temp ~%BASH_FOR_RES%~ BEGIN
                        REPLACE_TEXTUALLY ~%var%$~ ~~
                    END
                    ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%temp%~)) BEGIN
                        OUTER_TEXT_SPRINT output $EVAL ~remapped_tis~(~%temp%~)
                        //MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%output%%var%.%BASH_FOR_EXT%~
						COPY ~%MOD_FOLDER%/base/bmpLM/%temp%LM.bmp~ ~override/%output%LM.bmp~
                        OUTER_SET updated = 1
                    END
                END
            END
        END
	END
END


