BACKUP ~bggoEET/backup~
SUPPORT ~http://www.shsforums.net/forum/641-bg-graphics-overhaul/~
//AUTHOR ~Yovaneth, EET support by White Agnus, fixes by TotoR and weigo~
VERSION ~v2.0 beta~

ALWAYS
	INCLUDE ~bggoEET/lib/a7_tools.tpa~
	INCLUDE ~bggoEET/lib/macros.tpa~
	INCLUDE ~bggoEET/lib/eet_cpmvars.tpa~
	INCLUDE ~bggoEET/lib/g3_bgt_cpmvars.tpa~
	INCLUDE ~bggoEET/lib/g3_tutu_cpmvars.tpa~

	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SPRINT ee ~ee~
	END

	ACTION_IF GAME_IS ~bgt tutu_totsc~ BEGIN
		OUTER_SPRINT ee ~non-ee~
		MKDIR ~bggoEET/base/tbcD/BG0900~
		COPY_LARGE ~bggoEET/base/bridge/tbcD~ ~bggoEET/base/tbcD/BG0900~
		COPY_LARGE ~bggoEET/base/bridge/tbcN~ ~bggoEET/base/tbcN/BG0900~
		COPY + ~bggoEET/base/bridge/wedD~ ~bggoEET/base/wedD~
		COPY + ~bggoEET/base/bridge/wedN~ ~bggoEET/base/wedN~
		COPY + ~bggoEET/base/bridge/mosN~ ~bggoEET/base/mosN/%ee%~
		COPY + ~bggoEET/base/bridge/mosD~ ~bggoEET/base/mosD/%ee%~
		COPY + ~bggoEET/base/bridge/bmpN~ ~bggoEET/base/bmpN~
	END
END

BEGIN ~Purple Patches~
LABEL ~BGGOEET-PurplePatches~
SUBCOMPONENT ~Baldur's Gate Graphics Overhaul for EET~
REQUIRE_PREDICATE GAME_IS ~eet bgt tutu_totsc~ ~This mod is currently for EET and BGT only~

ACTION_FOR_EACH are IN ~BG0100~ ~BG0200~ ~BG0300~ ~BG0600~ ~BG0621~ ~BG0700~ ~BG0800~ ~BG1100~ ~BG1200~ ~BG1300~ BEGIN
	LAF ~INSTALL_TILESETS~ STR_VAR purple = ~PP~ END
END

LAF ~INSTALL_MAIN~ END

BEGIN ~No Purple Patches~
LABEL ~BGGOEET-Main~
SUBCOMPONENT ~Baldur's Gate Graphics Overhaul for EET~
REQUIRE_PREDICATE GAME_IS ~eet bgt tutu_totsc~ ~This mod is currently for EET and BGT only~

ACTION_FOR_EACH are IN ~BG0100~ ~BG0200~ ~BG0300~ ~BG0600~ ~BG0621~ ~BG0700~ ~BG0800~ ~BG1100~ ~BG1200~ ~BG1300~ BEGIN
	LAF ~INSTALL_TILESETS~ STR_VAR purple = ~P~ END
END

LAF ~INSTALL_MAIN~ END

BEGIN ~Use extended BG1 areas for TDDz~
LABEL ~BGGOEET-TDDzExtendedAreas~
REQUIRE_PREDICATE (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "2") OR (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "3") OR (MOD_IS_INSTALLED "TDDZ/SETUP-TDDZ.TP2" "4") "No TDD component detected."
REQUIRE_COMPONENT "BGGOEET/SETUP-BGGOEET.TP2" "0" "The main component must be installed before" 

ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_tis_tdd BEGIN
	"DD1900"    => "%BanditCamp%"
	"DD2300"    => "%FriendlyArmInn%"
	"DD3300"    => "%Beregost%"
	"DD8000"    => "%Larswood%"
END

ACTION_PHP_EACH remapped_tis_tdd AS TDDARE => BG1ARE BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%TDDARE%.are~ BEGIN
		COPY_EXISTING ~%TDDARE%.are~ ~override~
		PATCH_IF SOURCE_SIZE > 0x11b BEGIN
			WRITE_ASCIIE 0x8 ~%BG1ARE%~ #8
			WRITE_BYTE 0x48 (THIS BOR 0b01000000)
		END
		BUT_ONLY
	END
END
