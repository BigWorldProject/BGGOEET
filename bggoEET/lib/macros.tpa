ACTION_DEFINE_ASSOCIATIVE_ARRAY remapped_tis BEGIN
	"BG0100"    => "%NWBaldursGate%"
	"BG0101"    => "%NWBaldursGate_SilvershieldEstate_L1%"
	"BG0102"    => "%NWBaldursGate_SilvershieldEstate_L2%"
	"BG0103"    => "%NEBaldursGate_SplurgingSturgeon_L1%"
	"BG0130"    => "%WBaldursGate_HallofWonders%"
	"BG0131"    => "%WBaldursGate_HighHouseofWonders%"
	"BG0162"    => "%NWBaldursGate_LaertasHouse_L1%"
	"BG0166"    => "%NWBaldursGate_Tavern_L2%"
	"BG0200"    => "%NBaldursGate%"
	"BG0224"    => "%WSewers%"
	"BG0225"    => "%CentralSewers%"
	"BG0300"    => "%NEBaldursGate%"
	"BG0400"    => "%Farmlands%"
	"BG0500"    => "%DurlagsTower%"
	"BG0600"    => "%WBaldursGate%"
	"BG0621"    => "%BaldursGateDocks_IronThroneRoof%"
	"BG0700"    => "%CentralBaldursGate%"
	"BG0719"    => "%CentralBaldursGate_FeloniusManor%"
	"BG0800"    => "%EBaldursGate%"
	"BG0900"    => "%WyrmsCrossing%"
	"BG1000"    => "%UlgothsBeard%"
	"BG1100"    => "%SWBaldursGate%"
	"BG1101"    => "%SWBaldursGate_WivensHouse_L1%"
	"BG1102"    => "%SWBaldursGate_WivensHouse_L2%"
	"BG1200"    => "%BaldursGateDocks%"
	"BG1207"    => "%BaldursGateDocks_BasiliskWarehouse%"
	"BG1208"    => "%BaldursGateDocks_NoraleesWarehouse%"
	"BG1300"    => "%SEBaldursGate%"
	"BG1320"    => "%SEBaldursGate_GeneralStore1%"
	"BG1400"    => "%FishingVillage%"
	"BG1500"    => "%IsleofBalduranN%"
	"BG1600"    => "%CloakwoodDruids%"
	"BG1700"    => "%CloakwoodWyverns%"
	"BG1800"    => "%CloakwoodMines%"
	"BG1900"    => "%BanditCamp%"
	"BG2000"    => "%IsleofBalduranS%"
	"BG2100"    => "%CloakwoodNest%"
	"BG2200"    => "%CloakwoodLodge%"
	"BG2300"    => "%FriendlyArmInn%"
	"BG2400"    => "%Peldvale%"
	"BG2600"    => "%Candlekeep%"
	"BG2700"    => "%LionsWay%"
	"BG2800"    => "%CoastWay%"
	"BG2900"    => "%Larswood%"
	"BG3000"    => "%SpiderWood%"
	"BG3100"    => "%ShipwrecksCoast%"
	"BG3200"    => "%HighHedge%"
	"BG3202"    => "%HighHedge_ThalanthyrsAbode%"
	"BG3300"    => "%Beregost%"
	"BG3400"    => "%Temple%"
	"BG3500"    => "%MutaminsGarden%"
	"BG3600"    => "%Lighthouse%"
	"BG3700"    => "%RedCanyons%"
	"BG3800"    => "%SouthBeregostRoad%"
	"BG3900"    => "%Ulcaster%"
	"BG4000"    => "%Gullykin%"
	"BG4100"    => "%ArchaeologicalSite%"
	"BG4200"    => "%FishermansLake%"
	"BG4300"    => "%NorthNashkelRoad%"
	"BG4400"    => "%LonelyPeaks%"
	"BG4500"    => "%FirewineBridge%"
	"BG4600"    => "%BearRiver%"
	"BG4700"    => "%XvartVillage%"
	"BG4800"    => "%Nashkel%"
	"BG4900"    => "%NashkelCarnival%"
	"BG5000"    => "%ValleyoftheTombs%"
	"BG5100"    => "%GnollStronghold%"
	"BG5200"    => "%DryadFalls%"
	"BG5300"    => "%FireLeafForest%"
	"BG5400"    => "%NashkelMines%"
	"BG5500"    => "%GibberlingMountains%"
	"BG5600"    => "%Encounter_Plains1%"
	"BG5700"    => "%Encounter_Canyon1%"
	"BG5800"    => "%Encounter_Cliff1%"
	"BG5900"    => "%Encounter_Grassland1%"
	"BG6000"    => "%Encounter_Forest1%"
	"BG6100"    => "%Encounter_Road%"
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN
		bird => 1
		eagle => 1
		vultur => 1
		seagul => 1
END

DEFINE_ACTION_FUNCTION ~INSTALL_TILESETS~
	STR_VAR
		purple = ~~
BEGIN

	MKDIR ~bggoEET/bif~
	
	ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%are%~)) BEGIN
	OUTER_TEXT_SPRINT are2 $EVAL ~remapped_tis~(~%are%~)
	
	ACTION_IF FILE_EXISTS_IN_GAME ~%are2%.are~ BEGIN
		COPY_EXISTING ~%are2%.are~ ~override~
		PATCH_IF SOURCE_SIZE > 0x11b BEGIN // Valid size check
			WRITE_BYTE 0x48 (THIS BOR 0b01000000) // Toggle extended night flag on whether on or off
			GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110
			PHP_EACH actors AS i => off BEGIN
				READ_ASCII off + 0x80 cre_resref
				TO_LOWER cre_resref
				PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
					WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
				END
			END
		END
		BUT_ONLY
		
		MKDIR ~bggoEET/bif/%are2%~
		

		ACTION_IF FILE_EXISTS ~bggoEET/base/tbcD/%are%/%are%.tbc~ BEGIN
			LAF HANDLE_TILECONV
				STR_VAR
					input_path    = EVAL ~bggoEET/base/tbcD/%are%~
					output_path	  = EVAL ~bggoEET/bif/%are2%~
			END		
			ACTION_IF !GAME_IS ~eet~ BEGIN
			MOVE + ~bggoEET/bif/%are2%/%are%.tis~ ~bggoEET/bif/%are2%/%are2%.tis~
			END
		
			//COPY ~bggoEET/base/mosD/%ee%/%are%.mos~ ~bggoEET/bif/%are2%/%are2%.mos~
			LAF HANDLE_TILECONV
				STR_VAR
					input_path = EVAL ~bggoEET/base/mbcD/%ee%/%are%~
					output_path	  = EVAL ~bggoEET/bif/%are2%~
			END
			ACTION_IF !GAME_IS ~eet~ BEGIN
				MOVE + ~bggoEET/bif/%are2%/%are%.mos~ ~bggoEET/bif/%are2%/%are2%.mos~
			END
		
			ACTION_IF FILE_EXISTS ~bggoEET/base/wedD/%are%.wed~ BEGIN
				COPY ~bggoEET/base/wedD/%are%.wed~ ~bggoEET/bif/%are2%/%are2%.wed~
				REPLACE_TEXTUALLY ~%are%~ ~%are2%~
			END ELSE BEGIN
				COPY_EXISTING ~%are2%.wed~ ~bggoEET/bif/%are2%~
			END
		
			COPY_EXISTING ~%are2%LM.bmp~ ~bggoEET/bif/%are2%~
		END		
		
		LAF HANDLE_TILECONV
			STR_VAR
				input_path    = EVAL ~bggoEET/base/tbcN%purple%/%are%~
				output_path	  = EVAL ~bggoEET/bif/%are2%~
		END
		ACTION_IF !GAME_IS ~eet~ BEGIN
		MOVE + ~bggoEET/bif/%are2%/%are%N.tis~ ~bggoEET/bif/%are2%/%are2%N.tis~
		END
		COPY ~bggoEET/base/wedN%purple%/%are%N.wed~ ~bggoEET/bif/%are2%/%are2%N.wed~
			REPLACE_TEXTUALLY ~%are%~ ~%are2%~
		PATCH_IF ((~%are%~ STRING_COMPARE_CASE "%Beregost%")=0 AND FILE_EXISTS ~override/GavinNPC.B~) BEGIN
			PATCH_PRINT ~Gavin detected~
			LPM ~patchGavin~
		END
			
		
		//COPY ~bggoEET/base/mosN%purple%/%ee%/%are%N.mos~ ~bggoEET/bif/%are2%/%are2%N.mos~
		LAF HANDLE_TILECONV
			STR_VAR
				input_path    = EVAL ~bggoEET/base/mbcN%purple%/%are%~
				output_path	  = EVAL ~bggoEET/bif/%are2%~
		END
		ACTION_IF !GAME_IS ~eet~ BEGIN
			MOVE + ~bggoEET/bif/%are2%/%are%N.mos~ ~bggoEET/bif/%are2%/%are2%N.mos~
		END
		
		COPY ~bggoEET/base/bmpN/%are%LN.bmp~ ~bggoEET/bif/%are2%/%are2%LN.bmp~
		
		ACTION_IF FILE_EXISTS ~bggoEET/base/bmpSR/%are%SR.bmp~ BEGIN
			COPY ~bggoEET/base/bmpSR/%are%SR.bmp~ ~bggoEET/bif/%are2%/%are2%SR.bmp~
		END ELSE BEGIN
		COPY_EXISTING ~%are2%SR.bmp~ ~bggoEET/bif/%are2%~
		END
		
		COPY_EXISTING ~%are2%HT.bmp~ ~bggoEET/bif/%are2%~
		MAKE_BIFF ~go%are2%~ BEGIN ~bggoEET/bif/%are2%~ ~^.*$~ END

		ACTION_FOR_EACH var IN bmp mos tis wed BEGIN
			ACTION_BASH_FOR ~bggoEET/bif/%are2%~ ~^.+\.%var%$~ BEGIN
				DELETE +  ~%BASH_FOR_FILESPEC%~
			END
		END

		LAF REMOVE_DIRECTORY STR_VAR dir_name = EVAL ~bggoEET/bif/%are2%~ END
	END
	
	LAF REMOVE_DIRECTORY STR_VAR dir_name = ~bggoEET/bif~ END
	
END
END

DEFINE_ACTION_FUNCTION ~INSTALL_MAIN~
BEGIN
	MKDIR ~bggoEET/bif~

	LAF HANDLE_TILECONV
		STR_VAR
			input_path    = ~bggoEET/base/tbcN/ys~
			output_path	  = ~bggoEET/bif/bggom~
	END

	COPY ~bggoEET/base/bam~ ~bggoEET/bif/bggom~

	MAKE_BIFF ~goMAIN~ BEGIN ~bggoEET/bif/bggom~ ~^.*$~ END

	ACTION_FOR_EACH var IN bam bmp mos tis wed BEGIN
		ACTION_BASH_FOR ~bggoEET/bif/bggom~ ~^.+\.%var%$~ BEGIN
			DELETE +  ~%BASH_FOR_FILESPEC%~
		END
	END

	LAF REMOVE_DIRECTORY STR_VAR dir_name = ~bggoEET/bif/bggom~ END

	LAF REMOVE_DIRECTORY STR_VAR dir_name = ~bggoEET/bif~ END
	
	ACTION_FOR_EACH are IN ~BG0400~ ~BG0500~ ~BG0900~ ~BG1000~ ~BG1400~ ~BG1500~ ~BG1600~ ~BG1700~ ~BG1800~ ~BG1900~ ~BG2000~ ~BG2100~ ~BG2200~ ~BG2300~ ~BG2400~ ~BG2600~ ~BG2700~ ~BG2800~ ~BG2900~ ~BG3000~ ~BG3100~ ~BG3200~ ~BG3300~ ~BG3400~ ~BG3500~ ~BG3600~ ~BG3700~ ~BG3800~ ~BG3900~ ~BG4000~ ~BG4100~ ~BG4200~ ~BG4300~ ~BG4400~ ~BG4500~ ~BG4600~ ~BG4700~ ~BG4800~ ~BG4900~ ~BG5000~ ~BG5100~ ~BG5200~ ~BG5300~ ~BG5400~ ~BG5500~ ~BG5600~ ~BG5700~ ~BG5800~ ~BG5900~ ~BG6000~ ~BG6100~ BEGIN
		LAF ~INSTALL_TILESETS~ STR_VAR purple = ~~ END 
	END
	
	//**************************************************
	// Add torches to the temple in Candlekeep
	//**************************************************
	ACTION_DEFINE_ARRAY ccxs BEGIN 3933 4086 4201 4321 END
	ACTION_DEFINE_ARRAY ccys BEGIN 416 253 323 360 END

	ACTION_PHP_EACH ccxs AS index => ccx BEGIN
	  COPY_EXISTING "%Candlekeep%.are" override
					"%Candlekeep_Ch6%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ccxs("%index%")
			fj_loc_y = $ccys("%index%")
			fj_schedule = 0b111111111111111111111111 //Hours always
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Torch
			fj_bam_resref = flamblu2
		END
	  BUT_ONLY
	END
	
	//**************************************************
	// Add some braziers in Baldur's Gate
	//**************************************************
	ACTION_DEFINE_ARRAY ar1xs BEGIN 2853 2246 133 310 386 2165 END
	ACTION_DEFINE_ARRAY ar1ys BEGIN 499 555 1645 2295 3155 1279 END

	ACTION_PHP_EACH ar1xs AS index => ar1x BEGIN
	  COPY_EXISTING "%NWBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar1xs("%index%")
			fj_loc_y = $ar1ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar2xs BEGIN 941 3314 END
	ACTION_DEFINE_ARRAY ar2ys BEGIN 80 207 END

	ACTION_PHP_EACH ar2xs AS index => ar2x BEGIN
	  COPY_EXISTING "%NBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar2xs("%index%")
			fj_loc_y = $ar2ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar2bxs BEGIN 2056 2251 END
	ACTION_DEFINE_ARRAY ar2bys BEGIN 1510 1699 END

	ACTION_PHP_EACH ar2bxs AS index => ar2bx BEGIN
	  COPY_EXISTING "%NBaldursGate%.are" override
		LPF fj_are_structure
			INT_VAR
			fj_loc_x = $ar2bxs("%index%")
			fj_loc_y = $ar2bys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = flame2l
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar3xs BEGIN 247 933 1583 2928 841 1124 1270 1369 1479 1735 END
	ACTION_DEFINE_ARRAY ar3ys BEGIN 361 428 529 2327 3130 2411 2108 1898 1659 1120 END

	ACTION_PHP_EACH ar3xs AS index => ar3x BEGIN
	  COPY_EXISTING "%NEBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar3xs("%index%")
			fj_loc_y = $ar3ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar6xs BEGIN 385 383 556 END
	ACTION_DEFINE_ARRAY ar6ys BEGIN 81 420 2478 END

	ACTION_PHP_EACH ar6xs AS index => ar6x BEGIN
	  COPY_EXISTING "%WBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar6xs("%index%")
			fj_loc_y = $ar6ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar7xs BEGIN 3186 3637 4121 END
	ACTION_DEFINE_ARRAY ar7ys BEGIN 1670 1566 1083 END

	ACTION_PHP_EACH ar7xs AS index => ar7x BEGIN
	  COPY_EXISTING "%CentralBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar7xs("%index%")
			fj_loc_y = $ar7ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
END

	ACTION_DEFINE_ARRAY ar8xs BEGIN 3311 3337 3422 853 715 585 168 END
	ACTION_DEFINE_ARRAY ar8ys BEGIN 2614 2392 295 42 203 446 814 END

	ACTION_PHP_EACH ar8xs AS index => ar8x BEGIN
	  COPY_EXISTING "%EBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar8xs("%index%")
			fj_loc_y = $ar8ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END

	ACTION_DEFINE_ARRAY ar11xs BEGIN 322 552 241 1309 2145 END
	ACTION_DEFINE_ARRAY ar11ys BEGIN 1172 1969 824 2810 2912 END

	ACTION_PHP_EACH ar11xs AS index => ar11x BEGIN
	  COPY_EXISTING "%SWBaldursGate%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar11xs("%index%")
			fj_loc_y = $ar11ys("%index%")
			fj_schedule = 0b111000000000000000111111 //Hours 22.00 - 07.00
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Brazier
			fj_bam_resref = fire_3
		END
	  BUT_ONLY
	END
	
	//**************************************************
	// Light the wall sconces in the Ducal Palace
	//**************************************************
	ACTION_DEFINE_ARRAY ar607xs BEGIN 226 305 420 542 671 1170 1260 1346 1438 1546 1492 1406 1316 1291 1209 1405 1533 1654 860 803 713 627 481 566 656 END
	ACTION_DEFINE_ARRAY ar607ys BEGIN 434 355 397 457 522 459 368 282 217 271 354 440 531 844 927 883 947 1007 970 1049 1141 1226 1155 1069 978 END

	ACTION_PHP_EACH ar607xs AS index => ar607x BEGIN
	  COPY_EXISTING "%SWBaldursGate_FlamingFistHQ_L1%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar607xs("%index%")
			fj_loc_y = $ar607ys("%index%")
			fj_schedule = 0b111111111111111111111111 //Hours All
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Sconce
			fj_bam_resref = flame2m
		END
	  BUT_ONLY
	END

	//**************************************************
	// Light the candles in the Ducal Palace
	//**************************************************
	ACTION_DEFINE_ARRAY ar607cxs BEGIN 871 896 1031 1003 END
	ACTION_DEFINE_ARRAY ar607cys BEGIN 769 744 810 836 END

	ACTION_PHP_EACH ar607cxs AS index => ar607cx BEGIN
	  COPY_EXISTING "%SWBaldursGate_FlamingFistHQ_L1%.are" override
		LPF fj_are_structure
		  INT_VAR
			fj_loc_x = $ar607cxs("%index%")
			fj_loc_y = $ar607cys("%index%")
			fj_schedule = 0b111111111111111111111111 //Hours All
			fj_flags = 0b00000000000000000001000001000011	// Visible, blend colours, not covered by wall, don't remove in combat
			STR_VAR
			fj_structure_type = animation
			fj_name = Candle
			fj_bam_resref = flame2s
		END
	  BUT_ONLY
	END
	
	ACTION_BASH_FOR ~bggoEET/base/bmpLM~ ~^.+$~ BEGIN
		ACTION_TO_UPPER BASH_FOR_RES
        ACTION_IF ~%BASH_FOR_EXT%~ STRING_EQUAL_CASE ~bmp~ BEGIN //BMP
            OUTER_SET updated = 0
            ACTION_FOR_EACH var IN LM BEGIN
                ACTION_IF (updated = 0) BEGIN
                    OUTER_PATCH_SAVE temp ~%BASH_FOR_RES%~ BEGIN
                        REPLACE_TEXTUALLY ~%var%$~ ~~
                    END
                    ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%temp%~)) BEGIN
                        OUTER_TEXT_SPRINT output $EVAL ~remapped_tis~(~%temp%~)
                        //MOVE + ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_DIRECTORY%/%output%%var%.%BASH_FOR_EXT%~
						COPY ~bggoEET/base/bmpLM/%temp%LM.bmp~ ~override/%output%LM.bmp~
                        OUTER_SET updated = 1
                    END
                END
            END
        END
	END
	
	COPY_EXISTING ~%Candlekeep_Ch6%.are~ ~override~
		PATCH_IF SOURCE_SIZE > 0x11b BEGIN // Valid size check
			WRITE_BYTE 0x48 (THIS BOR 0b01000000) // Toggle extended night flag on whether on or off
			GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110
			PHP_EACH actors AS i => off BEGIN
				READ_ASCII off + 0x80 cre_resref
				TO_LOWER cre_resref
				PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
					WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
			END
		END
	END
	BUT_ONLY
	
END

DEFINE_PATCH_MACRO ~patchGavin~ BEGIN
sz = SOURCE_SIZE
		PATCH_IF sz > 0x48 BEGIN
			READ_LONG 0x8 ovr_count //Read initial offsets
			READ_LONG 0xc door_count
			READ_LONG 0x10 ovr_offset
			READ_LONG 0x14 sec_hdr
			READ_LONG 0x18 door_offset
			READ_LONG 0x1c dtc_indices
			READ_LONG sec_hdr poly_count
			READ_LONG (sec_hdr + 4) poly_offset
			READ_LONG (sec_hdr + 8) vert_offset
			READ_LONG (sec_hdr + 0xc) wall_offset
			READ_LONG (sec_hdr + 0x10) poly_table
			new_door_offset = door_offset + door_count * 0x1a
			new_vert_index = ((SOURCE_SIZE - vert_offset) / 4)

			INSERT_BYTES sz 0x20 //Insert 8 new vertex pairs
			WRITE_SHORT sz 2370 //Open door
			WRITE_SHORT (sz + 2) 1563
			WRITE_SHORT (sz + 4) 2325
			WRITE_SHORT (sz + 6) 1563
			WRITE_SHORT (sz + 8) 2325
			WRITE_SHORT (sz + 0xa) 1478
			WRITE_SHORT (sz + 0xc) 2370
			WRITE_SHORT (sz + 0xe) 1478
			WRITE_SHORT (sz + 0x10) 2370 //Closed door
			WRITE_SHORT (sz + 0x12) 1562
			WRITE_SHORT (sz + 0x14) 2330
			WRITE_SHORT (sz + 0x16) 1620
			WRITE_SHORT (sz + 0x18) 2330
			WRITE_SHORT (sz + 0x1a) 1536
			WRITE_SHORT (sz + 0x1c) 2370
			WRITE_SHORT (sz + 0x1e) 1478

			INSERT_BYTES poly_table 0x24 //Insert 2 new polygons
			WRITE_LONG poly_table new_vert_index //Starting vertex index
			WRITE_LONG (poly_table + 4) 4 //Vertex count
			WRITE_BYTE (poly_table + 8) 0x80 //Open door
			WRITE_BYTE (poly_table + 9) 0xff //Unknown
			WRITE_SHORT (poly_table + 0xa) 2325 //Boundary minimum X coordinate
			WRITE_SHORT (poly_table + 0xc) 2370 //Boundary maximum X coordinate
			WRITE_SHORT (poly_table + 0xe) 1478 //Boundary minimum Y coordinate
			WRITE_SHORT (poly_table + 0x10) 1563 //Boundary maximum Y coordinate
			WRITE_LONG (poly_table + 0x12) (new_vert_index + 4) //Starting vertex index
			WRITE_LONG (poly_table + 0x16) 4 //Vertex count
			WRITE_BYTE (poly_table + 0x1a) 0x80 //Closed door
			WRITE_BYTE (poly_table + 0x1b) 0xff //Unknown
			WRITE_SHORT (poly_table + 0x1c) 2330 //Boundary minimum X coordinate
			WRITE_SHORT (poly_table + 0x1e) 2370 //Boundary maximum X coordinate
			WRITE_SHORT (poly_table + 0x20) 1478 //Boundary minimum Y coordinate
			WRITE_SHORT (poly_table + 0x22) 1620 //Boundary maximum Y coordinate

			FOR (i = 0; i < door_count; i += 1) BEGIN //Update door polygon offsets
				WRITE_LONG (i * 0x1a + door_offset + 0x12) (THIS + 0x1a) //Open polygons offset
				WRITE_LONG (i * 0x1a + door_offset + 0x16) (THIS + 0x1a) //Closed polygons offset
				PATCH_IF (i = (door_count - 1)) BEGIN
					READ_SHORT (i * 0x1a + door_offset + 0xa) tile_index
					READ_SHORT (i * 0x1a + door_offset + 0xc) tile_count
				END
			END

			INSERT_BYTES new_door_offset 0x1a //Insert new door
			WRITE_ASCII new_door_offset ~Door4437~ //Name
			WRITE_SHORT (new_door_offset + 8) 1 //Closed
			WRITE_SHORT (new_door_offset + 0xa) (tile_index + tile_count) //Tile cell index
			WRITE_SHORT (new_door_offset + 0xe) 1 //Open polygons count
			WRITE_SHORT (new_door_offset + 0x10) 1 //Closed polygons count
			WRITE_LONG (new_door_offset + 0x12) (poly_table + 0x1a) //Open polygons offset
			WRITE_LONG (new_door_offset + 0x16) (poly_table + 0x1a + 0x12) //Closed polygons offset

			//Update offsets for new objects
			FOR (j = 0; j < ovr_count; j += 1) BEGIN //Update overlays
				WRITE_LONG (j * 0x18 + ovr_offset + 0x10) (THIS + 0x1a) //Tilemap offset
				WRITE_LONG (j * 0x18 + ovr_offset + 0x14) (THIS + 0x1a) //Tile index lookup offset
			END
			door_count += 1
			WRITE_LONG 0xc door_count
			WRITE_LONG 0x1c (dtc_indices + 0x1a)
			WRITE_LONG (sec_hdr + 4) (poly_offset + 0x1a)
			WRITE_LONG (sec_hdr + 8) (vert_offset + 0x1a + 0x24)
			WRITE_LONG (sec_hdr + 0xc) (wall_offset + 0x1a)
			WRITE_LONG (sec_hdr + 0x10) (poly_table + 0x1a + 0x24)
		END
END
